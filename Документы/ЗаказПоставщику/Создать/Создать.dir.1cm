// Создает новый документ ЗаказПоставщику по переданным параметрам
//
// Параметры:
// Структура параметров, полученная методом "Документы.ЗаказПоставщику.Создать.Параметры"
//
// Возврат:
// Номер созданного заказа

ГлавноеОкно.ВыполнитьКоманду ( "e1cib/data/Документ.ЗаказПоставщику" );

форма = Здесь ( "Заказ поставщику (соз*" );

// *********************************************
// Заполнение шапки
// *********************************************

дата = _.Дата;
если ( дата <> неопределено ) тогда
	Установить ( "!Дата", дата );
конецесли;
Установить ( "!Партнер", _.Поставщик );
Установить ( "!Организация", _.Организация );
склад = _.Склад;
если ( склад <> неопределено ) тогда
	Установить ( "!Склад", склад );
конецесли;

// *********************************************
// Заполнение товаров
// *********************************************

таблица = Фокус ( "!Товары" );
для каждого товар из _.Товары цикл

	Нажать ( "!ТоварыДобавить", таблица );
	Установить ( "!ТоварыНоменклатура", товар.Товар );
	Установить ( "!ТоварыКоличествоУпаковок", товар.Количество );
	Установить ( "!ТоварыЦена", товар.Цена );

конеццикла;

// *********************************************
// Запись документа, получение номера
// *********************************************

Нажать ( "!ФормаЗаписать" );
номер = Взять ( "!Номер" );

// *********************************************
// Проведение документа и возврат номера
// *********************************************

Нажать ( "!ФормаПровести" );

закрыть = не _.ОставитьОткрытым;
если ( закрыть ) тогда
	Закрыть ();
конецесли;

возврат номер;
