Вызвать ( "Общее.Начало" );
ЗакрытьВсё ();

// *********************************************
// Начальные данные
// *********************************************

метка = ТекущаяДата ();
поставщик = "_Тест заказа: " + метка;
организация = "Деловой союз";

// *********************************************
// Создание товаров
// *********************************************

товары = новый Массив ();
добавить ( товары, новыйТовар ( "_Товар1 " + метка ), 5, 150 );
добавить ( товары, новыйТовар ( "_Товар2 " + метка ), 15, 250 );
добавить ( товары, новыйТовар ( "_Товар3 " + метка ), 25, 350 );

// *********************************************
// Создание поставщика
// *********************************************

п = Вызвать ( "Справочники.Поставщики.Создать.Параметры" );
п.Наименование = поставщик;
п = Вызвать ( "Справочники.Поставщики.Создать", п );

// ***********************************************
// Создание, проведение и проверка движений заказа
// ***********************************************

п = Вызвать ( "Документы.ЗаказПоставщику.Создать.Параметры" );
п.Организация = организация;
п.Поставщик = поставщик;
п.Товары = товары;
п.ОставитьОткрытым = истина;
номер = Вызвать ( "Документы.ЗаказПоставщику.Создать", п );
Здесь ( "Заказ поставщику " + номер + "*" );
Вызвать ( "Общее.ПроверитьДвижения" );

// *********************************************
// Проверим работу документа отчетом
// *********************************************

Позвать ( "ЗадолженностьПоставщикам", поставщик );

// *********************************************
// Процедуры
// *********************************************

Функция новыйТовар ( Наименование )

	п = Вызвать ( "Справочники.Номенклатура.Создать.Параметры" );
	п.Наименование = Наименование;
	возврат Вызвать ( "Справочники.Номенклатура.Создать", п );

КонецФункции

Процедура добавить ( Товары, Код, Количество, Цена )

	п = Вызвать ( "Документы.ЗаказПоставщику.Создать.Строка" );
	п.Товар = Код;
	п.Количество = Количество;
	п.Цена = Цена;
	Товары.Добавить ( п );

КонецПроцедуры
